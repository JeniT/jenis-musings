<p>[UPDATE: Added a link to the full stylesheet, and edited the code so it doesn&#39;t overlap the right-hand column.]</p>

<p><a href="http://en.wikipedia.org/wiki/Levenshtein_distance" title="Wikipedia: Levenshtein distance">Levenshtein distance</a> is a measure of how many edits it takes to get from one string to another. In the basic algorithm, each addition, deletion and substitution counts as a single edit. So, for example, the distance between <code>&quot;XSLT 1.0&quot;</code> and <code>&quot;XSLT 2.0&quot;</code> is <code>1</code>: the only difference is the substitution of <code>2</code> for <code>1</code>, whereas the distance between <code>&quot;XSLT&quot;</code> and <code>&quot;XQuery&quot;</code> is <code>5</code>: three substitutions and two additions.</p>

<p>One of the interesting features of Levenshtein distance is that there&#39;s a fairly straight-forward <a href="http://en.wikipedia.org/wiki/Dynamic_programming" title="Wikipedia: Dynamic programming">dynamic programming</a> algorithm that can be used to calculate it. I thought it might be interesting to see what an XSLT 2.0 implementation might look like.</p>

<!--break-->

<p>In its naive form, the Levenshtein distance is calculated by saying:</p>

<ol>
<li>The distance between an empty string A and another string B is the number of characters in B (eg distance between <code>&#39;&#39;</code> and <code>&#39;foo&#39;</code> is <code>3</code>)</li>
<li>Otherwise, take the minimum of:

<ul>
<li> one plus the distance between string A and B except for its last character</li>
<li> one plus the distance between A except for its last character and string B</li>
<li> the distance between A except for its last character and B except for its last character, plus one if the last characters of the two strings aren&#39;t the same</li>
</ul></li>
</ol>

<p>In the normal case, then, for each distance you calculate, you have to calculate three other distances and take a minimum. A naive XSLT implementation would be:</p>
<div class="highlight"><pre><code class="text language-text" data-lang="text">&lt;xsl:function name=&quot;my:LevenshteinDistanceA&quot; 
  as=&quot;xs:integer&quot;&gt;
  &lt;xsl:param name=&quot;string1&quot; as=&quot;xs:string&quot; /&gt;
  &lt;xsl:param name=&quot;string2&quot; as=&quot;xs:string&quot; /&gt;
  &lt;xsl:sequence 
    select=&quot;my:LevenshteinDistanceA(
              string-to-codepoints($string1),
             string-to-codepoints($string2),
             string-length($string1),
             string-length($string2))&quot; /&gt;
&lt;/xsl:function&gt;

&lt;xsl:function name=&quot;my:LevenshteinDistanceA&quot; 
  as=&quot;xs:integer&quot;&gt;
  &lt;xsl:param name=&quot;chars1&quot; as=&quot;xs:integer+&quot; /&gt;
  &lt;xsl:param name=&quot;chars2&quot; as=&quot;xs:integer+&quot; /&gt;
  &lt;xsl:param name=&quot;i1&quot; as=&quot;xs:integer&quot; /&gt;
  &lt;xsl:param name=&quot;i2&quot; as=&quot;xs:integer&quot; /&gt;
  &lt;xsl:choose&gt;
    &lt;xsl:when test=&quot;$i1 = 0&quot;&gt;
      &lt;xsl:sequence select=&quot;$i2&quot; /&gt;
    &lt;/xsl:when&gt;
    &lt;xsl:when test=&quot;$i2 = 0&quot;&gt;
      &lt;xsl:sequence select=&quot;$i1&quot; /&gt;
    &lt;/xsl:when&gt;
    &lt;xsl:otherwise&gt;
      &lt;xsl:variable name=&quot;char1&quot; as=&quot;xs:integer&quot; 
        select=&quot;$chars1[$i1]&quot; /&gt;
      &lt;xsl:variable name=&quot;char2&quot; as=&quot;xs:integer&quot;
        select=&quot;$chars2[$i2]&quot; /&gt;
      &lt;xsl:variable name=&quot;deletion&quot; as=&quot;xs:integer&quot;
        select=&quot;my:LevenshteinDistanceA($chars1, $chars2, 
                                        $i1 - 1, $i2) + 
                1&quot; /&gt;
      &lt;xsl:variable name=&quot;insertion&quot; as=&quot;xs:integer&quot;
        select=&quot;my:LevenshteinDistanceA($chars1, $chars2, 
                                        $i1, $i2 - 1) + 
                1&quot; /&gt;
      &lt;xsl:variable name=&quot;substitution&quot; as=&quot;xs:integer&quot;
        select=&quot;my:LevenshteinDistanceA($chars1, $chars2, 
                                        $i1 - 1, $i2 - 1) +
                (if ($char1 eq $char2) then 0 else 1)&quot; /&gt;
      &lt;xsl:sequence select=&quot;min(($deletion, $insertion, 
                                 $substitution))&quot; /&gt;
    &lt;/xsl:otherwise&gt;
  &lt;/xsl:choose&gt; 
&lt;/xsl:function&gt;
</code></pre></div>
<p>This is horrendously slow for even moderate-length strings. In Saxon 8.9B, on my laptop, it will compare two five-character strings in 63ms on average; for ten-character strings it takes 
26 seconds!</p>

<p>If you look at the <a href="http://en.wikipedia.org/wiki/Levenshtein_distance" title="Wikipedia: Levenshtein distance">Wikipedia page on Levenshtein distance</a>, you&#39;ll see an algorithm that effectively does memoisation to address the performance problem. Memoisation is the technique of recording the result of a function for a given set of arguments, and then using that stored result rather than recalculating it each time. For the Levenshtein distance algorithm, this is done by building a matrix of the distances between all possible leading substrings of the strings you&#39;re comparing. So to compare <code>&quot;foo&quot;</code> and <code>&quot;bar&quot;</code>, you effectively compare <code>&quot;&quot;</code> and <code>&quot;&quot;</code>, <code>&quot;b&quot;</code>, <code>&quot;ba&quot;</code> and <code>&quot;bar&quot;</code>; <code>&quot;f&quot;</code> and <code>&quot;&quot;</code>, <code>&quot;b&quot;</code>, <code>&quot;ba&quot;</code> and <code>&quot;bar&quot;</code>; <code>&quot;fo&quot;</code> and <code>&quot;&quot;</code>, <code>&quot;b&quot;</code>, <code>&quot;ba&quot;</code> and <code>&quot;bar&quot;</code>; and <code>&quot;foo&quot;</code> and <code>&quot;&quot;</code>, <code>&quot;b&quot;</code>, and <code>&quot;ba&quot;</code>, recording the results as you go.</p>

<p>Here&#39;s the dynamic programming version of the function in XSLT 2.0:</p>
<div class="highlight"><pre><code class="text language-text" data-lang="text">&lt;xsl:function name=&quot;my:LevenshteinDistanceB&quot; 
  as=&quot;xs:integer&quot;&gt;
  &lt;xsl:param name=&quot;string1&quot; as=&quot;xs:string&quot; /&gt;
  &lt;xsl:param name=&quot;string2&quot; as=&quot;xs:string&quot; /&gt;
  &lt;xsl:sequence 
    select=&quot;my:LevenshteinDistanceB(
              string-to-codepoints($string1),
             string-to-codepoints($string2),
             1, 1,
             for $p in (0 to string-length($string1)) 
                return $p,
             1)&quot; /&gt;
&lt;/xsl:function&gt;

&lt;xsl:function name=&quot;my:LevenshteinDistanceB&quot; 
  as=&quot;xs:integer&quot;&gt;
  &lt;xsl:param name=&quot;chars1&quot; as=&quot;xs:integer+&quot; /&gt;
  &lt;xsl:param name=&quot;chars2&quot; as=&quot;xs:integer+&quot; /&gt;
  &lt;xsl:param name=&quot;i1&quot; as=&quot;xs:integer&quot; /&gt;
  &lt;xsl:param name=&quot;i2&quot; as=&quot;xs:integer&quot; /&gt;
  &lt;xsl:param name=&quot;lastRow&quot; as=&quot;xs:integer+&quot; /&gt;
  &lt;xsl:param name=&quot;thisRow&quot; as=&quot;xs:integer+&quot; /&gt;
  &lt;xsl:choose&gt;
    &lt;xsl:when test=&quot;$i1 &gt; count($chars1)&quot;&gt;
      &lt;xsl:choose&gt;
        &lt;xsl:when test=&quot;$i2 = count($chars2)&quot;&gt;
          &lt;xsl:sequence select=&quot;$thisRow[last()]&quot; /&gt;
        &lt;/xsl:when&gt;
        &lt;xsl:otherwise&gt;
          &lt;xsl:sequence 
            select=&quot;my:LevenshteinDistanceB(
                      $chars1, $chars2, 
                      1, $i2 + 1, 
                      $thisRow, ($i2 + 1))&quot; /&gt;
        &lt;/xsl:otherwise&gt;
      &lt;/xsl:choose&gt;
    &lt;/xsl:when&gt;
    &lt;xsl:otherwise&gt;
      &lt;xsl:variable name=&quot;char1&quot; as=&quot;xs:integer&quot; 
        select=&quot;$chars1[$i1]&quot; /&gt;
      &lt;xsl:variable name=&quot;char2&quot; as=&quot;xs:integer&quot; 
        select=&quot;$chars2[$i2]&quot; /&gt;
      &lt;xsl:variable name=&quot;deletion&quot; as=&quot;xs:integer&quot;
        select=&quot;$lastRow[$i1 + 1] + 1&quot; /&gt;
      &lt;xsl:variable name=&quot;insertion&quot; as=&quot;xs:integer&quot;
        select=&quot;$thisRow[last()] + 1&quot; /&gt;
      &lt;xsl:variable name=&quot;substitution&quot; as=&quot;xs:integer&quot;
        select=&quot;$lastRow[$i1] +
                (if ($char1 eq $char2) then 0 else 1)&quot; /&gt;
      &lt;xsl:variable name=&quot;cost&quot; 
        select=&quot;min(($deletion, $insertion, 
                     $substitution))&quot; /&gt;
      &lt;xsl:sequence 
        select=&quot;my:LevenshteinDistanceB(
                  $chars1, $chars2, 
                  $i1 + 1, $i2, 
                  $lastRow, ($thisRow, $cost))&quot; /&gt;
    &lt;/xsl:otherwise&gt;
  &lt;/xsl:choose&gt;
&lt;/xsl:function&gt;
</code></pre></div>
<p>This performs <em>much</em> better. Here&#39;s a little table showing the results:</p>

<table>
  <tr><th># characters</th><th>time</th></tr>
  <tr><td>5</td><td>33ms</td></tr>
  <tr><td>10</td><td>50ms</td></tr>
  <tr><td>15</td><td>67ms</td></tr>
  <tr><td>20</td><td>77ms</td></tr>
</table>

<p>Why do I end there, when the timings are by no means unreasonable? Well, the bad news is that with anything over 20 characters, Saxon bails with a &quot;Too many nested function calls. May be due to infinite recursion.&quot; error. Which isn&#39;t unreasonable I guess, given that, with 20 characters, you&#39;re calling the same function 400 times in a row, but is kinda annoying given the recursion is not actually infinite.</p>

<p>I know David C.&#39;s done an implementation as well, which I hope he&#39;ll post in a comment, and I&#39;ll be interested to see whether that (or any other implementation) performs any better (both in terms of speed and in terms of not running out of stack). When we&#39;ve had a look at them, I&#39;ll try to put together some suitably gnomic comments on the design of recursive functions and templates in general...</p>

<p>You can <a href="http://www.jenitennison.com/blog/files/LevenshteinDistance.xsl" title="LevenshteinDistance.xsl">download the full stylesheet</a> if you want.</p>
